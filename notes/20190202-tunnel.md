# Figuring out the math difference in `tunnel`

The shading routine, copied from C++, behaves differently in Rust. Given that it
*deliberately exploits technically undefined behavior* in C++, this isn't
shocking -- I doubt it would even complete in a debug build.

But why, in a release build, is it producing different results?

Let's investigate.

Here it is in Rust:

```rust
fn shade(distance: f32, pixel: u8) -> u8 {
    let sel = (distance / (table::TEX_REPEAT_D * 2) as f32) as u32;
    let sel = sel.min(7).max(0);  // sure hope this generates USAT
    let r = (pixel as u32 >> (0x01010000_u32 >> (sel * 8)))
        & (0x5555AAFF_u32 >> (sel * 8));
    r as u8
}
```

And the original:

```c++
ETL_INLINE
static uint_fast8_t shade(float distance,
                          uint_fast8_t pixel) {
  unsigned sel = unsigned(distance / (config::texture_repeats_d * 2));
  // Clamp value as signed signed to trigger GCC's usat matching pattern (bug).
  sel = unsigned(etl::math::clamp(int(sel), 0, 7));

  return (pixel >> (0x01010000u >> (sel * 8)))
       & (0x5555AAFFu >> (sel * 8));
}
```

I see no obvious infidelity; the changes are all type coercions that C++ did
implicitly. Since Rust's rules on this prevent me from losing precision by
accident (unlike another language I could name) I'm pretty sure it's not
something like that.

How's it compile? Aggressively inlined is the short version. Here's a trace of
the upwards cone for the shader routine, so far as I can find it:

```
  ; LLVM is pleasantly good at mise-en-place-ing its constants.
 8001004:       f64a 27ff       movw    r7, #43775      ; 0xaaff
 8001022:       f2c5 5755       movt    r7, #21845      ; 0x5555
 8001036:       f04f 0807       mov.w   r8, #7
 800103c:       f04f 0918       mov.w   r9, #24

  ; a multiply and then converting out of floating point -- we've likely found
  ; the division producing `sel`.
 8001172:       ee20 ea8b       vmul.f32        s28, s1, s22
 8001182:       eebc eace       vcvt.u32.f32    s28, s28
 8001196:       ee1e 5a10       vmov    r5, s28

 80011b0:       2d07            cmp     r5, #7
 80011b2:       bf28            it      cs
 80011b4:       4645            movcs   r5, r8
  ; at this point r5 contains sel, saturated to 0..7.
  ; llvm has exploited the fact that vcvt saturates. Very nice.
  ; Moreover, Rust's f32 -> u32 conversion saturates! It's undefined in C.
  ; So we don't strictly speaking need USAT.
  ; that conditional move is costly, though, should look at the MInst tables
  ; and figure out how to get it to USAT. Anyway.

 80011b6:       ea09 05c5       and.w   r5, r9, r5, lsl #3
  ; Man, textual shift mnemonics are really rough on those of us with spatial
  ; dyslexia. That's `<< 3` for the record.
  ; 

 80011ba:       b2c0            uxtb    r0, r0

 80011bc:       fa26 f405       lsr.w   r4, r6, r5
 80011c0:       fa27 f505       lsr.w   r5, r7, r5
  ; r5 = (0x5555AAFF_u32 >> (sel * 8))

 80011c4:       f004 041f       and.w   r4, r4, #31
  ; r4 is now the pixel shift distance.
  ; Which, you'll note, is being suspiciously calculated mod 32. And generating
  ; additional instructions to do so. So that explains it. Some combination of
  ; rustc and llvm are conspiring to make this act like an intel right shift.

  ; final pixel shift:
 80011c8:       40e0            lsrs    r0, r4
  ; and final pixel mask:
 80011ca:       4028            ands    r0, r5
 80011f8:       f80e 0c03       strb.w  r0, [lr, #-3]
```

Well. I am technically exploiting undefined behavior in Rust, so I should not be
upset that the compiler has taken the liberty of changing my math. Sigh.

I wonder. The shift distances I'm using wind up being limited to 0..63 in
multiples of 8 bits. Would it be enough to go through `u64`? How would the
generated code look? For the record the shade function is now...

```rust
fn shade(distance: f32, pixel: u8) -> u8 {
    let sel = (distance / (table::TEX_REPEAT_D * 2) as f32) as u32;
    let sel = sel.min(7);  // sure hope this generates USAT
    let r = (pixel as u64 >> (0x01010000_u64 >> (sel * 8)))
        & (0x5555AAFF_u64 >> (sel * 8));
    r as u8
}
```

The visual results are now fixed.

```
 80011a8:       f000 003f       and.w   r0, r0, #63     ; 0x3f
  ; limit shift distance to 0..63

 80011b0:       f1a0 0720       sub.w   r7, r0, #32
  ; kind of a roundabout way of testing if it's > 31

 80011da:       eebc cacc       vcvt.u32.f32    s24, s24
 80011de:       2f00            cmp     r7, #0
  ; compare to 32

 80011e2:       ee1c 6a10       vmov    r6, s24
  ; get 
 80011fa:       ea86 0604       eor.w   r6, r6, r4
  ; XOR pattern
 80011fe:       b2f6            uxtb    r6, r6
  ; discard top 24 bits
 8001204:       fa26 f000       lsr.w   r0, r6, r0
  ; shift right by r0
 8001208:       bfa8            it      ge
  ; if we shifted by >= 32
 800120a:       2000            movge   r0, #0
  ; "shift in zeroes"
  ; note that this is conservative on this architecture.

 8001210:       4008            ands    r0, r1

 8001212:       f80e 0c03       strb.w  r0, [lr, #-3]

```

Hm. Okay, the numbers are right, but it's generating two cycles of crap per
shaded pixel. Concretely, measurements say rendering speed changes from 9.547ms
to 12.85ms -- that's a 35% hit.

Don't promote `pixel` to `u64`. In fact, don't promote `pixel` at all -- it was
becoming `u32` because C++'s shift operations aren't defined on `u8`, but Rust's
are.

```rust
fn shade(distance: f32, pixel: u8) -> u8 {
    let sel = (distance / (table::TEX_REPEAT_D * 2) as f32) as u32;
    let sel = sel.min(7);  // sure hope this generates USAT
    (pixel >> (0x01010000_u64 >> (sel * 8)))
        & (0x5555AAFF_u64 >> (sel * 8)) as u8
}
```

11.25ms -- 17% down.

Alright, let's consider what we're actually trying to achieve, instead of
tweaking my original UB hack.

What we're doing is, considering the *first* `sel` definition,
- 0 leaves things unchanged.
- 1 masks out LSBs.
- 2 and 3 shifts MSBs into LSBs and clears MSBs.
- anything over 3 produces black.

So I ought to be able to get the shifts into UB territory by recognizing the
greater-than-3 property.

```rust
fn shade(distance: f32, pixel: u8) -> u8 {
    let sel = (distance / (table::TEX_REPEAT_D * 2) as f32) as u32;
    if sel > 3 {
        0
    } else {
        // sel is 0..4
        let sel = sel * 8; // sel is 0..32, shifts should be UB
        (pixel >> (0x01010000_u32 >> sel))
            & (0x5555AAFF_u32 >> sel) as u8
    }
}
```

Does the compiler agree? Disassembly suggests that it does:

```
 800128e:       ea09 01c1       and.w   r1, r9, r1, lsl #3

 800129e:       fa28 f601       lsr.w   r6, r8, r1
 80012a2:       f006 0607       and.w   r6, r6, #7

 80012a6:       fa25 f101       lsr.w   r1, r5, r1

 80012be:       ee1f 4a10       vmov    r4, s30
 80012c2:       4044            eors    r4, r0
 80012c4:       b2e4            uxtb    r4, r4
 80012c6:       40f4            lsrs    r4, r6
 80012c8:       400c            ands    r4, r1
 80012ca:       f807 4c02       strb.w  r4, [r7, #-2]

```

The results are right, but we haven't recovered all of our performance, in part
because there's now a legit conditional in our path:

```
 8001276:       ee1f 1a10       vmov    r1, s30
 800127a:       2904            cmp     r1, #4
 800127c:       d305            bcc.n   800128a <tunnel::cclos4g0x543bht5::{{closure}}+0x29a>
 800127e:       eb02 000b       add.w   r0, r2, fp
 8001282:       2100            movs    r1, #0
 8001284:       f800 1c02       strb.w  r1, [r0, #-2]
 8001288:       e027            b.n     80012da <tunnel::cclos4g0x543bht5::{{closure}}+0x2ea>
 800128a:       ee3b fa4e       vsub.f32        s30, s22, s28
```

rustc/llvm are fairly literal about if statements. They seem to always assume
the "true" branch is more likely. As a result, *simply inverting the condition*
gets us this:

```
 800125e:       ee1f 1a10       vmov    r1, s30
 8001262:       2904            cmp     r1, #4
 8001264:       d228            bcs.n   80012b8 <tunnel::wwggpwe761253751::{{closure}}+0x2c8>
 8001266:       ee3b fa4e       vsub.f32        s30, s22, s28
```

...but then it branches around the `else`. Damnit, why are these not totally out
of line, LLVM? Why?

We're still around 11.2ms.


Okay, other opportunities for improvement in the source. The Rust computes the
XOR texture using this routine:

```rust
fn tex_fetch(x: f32, y: f32) -> u8 {
    (x as u32 ^ y as u32) as u8
}
```

I've noticed in the disassembly that the compiler is taking that quite
literally. (See the `uxtb` right smack dab in the middle of the critical path
in the listing?) The C++ used `uint8_fast_t` here, which is `u32` on Cortex-M.

The cheapest way to do the `u32` to `u8` conversion is to let `strb` ignore the
top 24 bits of a register. Let's move the truncation all the way out to the
framebuffer write.

This gets us to 10.71ms, or 12% down. Getting there.

---

For the record, if we don't shade at all, rendering takes 5.82ms. We're
seriously spending about 5ms/frame darkening some center pixels in the screen.


